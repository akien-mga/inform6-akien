!% -SD
!=======================================================================================
! Licence BSD
! Copyright (c) 2008 Rémi Verschelde (Akien) - http://ifiction.free.fr
!
! Tous droits réservés.
! La redistribution et l'utilisation sous forme binaire ou de code source,
! avec ou sans modification, est autorisée à condition que les conditions
! suivantes soient respectées:
!
! 1. Les redistributions du code source doivent conserver l'indication de
!    copyright ci-dessus, cette liste de conditions et la renonciation suivante.
! 2. Les redistributions sous forme binaire doivent reproduire l'indication
!    de copyright ci-dessus, cette liste de conditions et la renonciation suivante
!    dans la documentation et/ou les autres accessoires fournis avec la distribution.
! 3. Toutes les publicités mentionnant les caractéristiques ou l'utilisation
!    de ce logiciel doivent mentionner la phrase suivante :
!    "Ce produit inclus un logiciel et du travail développés par Rémi Verschelde (Akien) et
!    la communauté de la fiction interactive francophone (http://ifiction.free.fr/)
!     et internationale, ainsi que par les créateurs du système INFORM
!    (http://www.inform-fiction.org)."
! 4. Les noms des créateurs et développeurs ne peuvent être utilisés pour
!    approuver ou promouvoir les produits dérivés de ce logiciel sans une
!    permission écrite spécifique préalable.
!
! CE LOGICIEL EST FOURNI PAR LES TITULAIRES DU COPYRIGHT ET LES PARTICIPANTS
! "TEL QUEL" ET TOUTES LES GARANTIES EXPLICITES OU IMPLICITES, Y COMPRIS,
! MAIS NON LIMITÉES A CELLES-CI, LES GARANTIES IMPLICITES DE COMMERCIALISATION
! ET D'ADÉQUATION À UN USAGE PARTICULIER SONT DÉNIÉES.
! EN AUCUN CAS LES TITULAIRES DU COPYRIGHT OU LES PARTICIPANTS NE PEUVENT
! ÊTRE TENUS POUR RESPONSABLE DES DOMMAGES DIRECTS, INDIRECTS, FORTUITS,
! PARTICULIERS, EXEMPLAIRES OU CONSÉCUTIFS À UNE ACTION (Y COMPRIS, MAIS NON
! LIMITÉS À CEUX-CI, L'ACQUISITION DE MARCHANDISES OU DE SERVICES DE REMPLACEMENT;
! LES PERTES D'UTILISATIONS, DE DONNÉES OU FINANCIÈRES; OU L'INTERRUPTION
! D'ACTIVITÉS) DE QUELQUE MANIÈRE QUE CES DOMMAGES SOIENT CAUSÉS ET CECI
! POUR TOUTES LES THÉORIES DE RESPONSABILITÉS, QUE CE SOIT DANS UN CONTRAT,
! POUR DES RESPONSABILITÉS STRICTES OU DES PRÉJUDICES (Y COMPRIS DUS À UNE
! NEGLIGENCE OU AUTRE CHOSE) SURVENANT DE QUELQUE MANIÈRE QUE CE SOIT EN DEHORS
! DE L'UTILISATION DE CE LOGICIEL, MÊME EN CAS D'AVERTISSEMENT DE LA POSSIBILITÉ
! DE TELS DOMMAGES.
!=======================================================================================

Constant Story "Rats";
Constant Headline
            "^Une fiction interactive de Rémi Verschelde (Akien).^^";
Release 1;  Serial "100808";

Constant SACK_OBJECT = hotte;

Constant TASKS_PROVIDED;
Constant NUMBER_TASKS = 4;
Constant MAX_SCORE = 30;
Array task_scores -> 8 5 2 5;

Include "Parser";
Include "VerbLib";

!=======================================================================================

Object  trou_dortoirs "Dortoirs du palais"
  with  name 'dortoirs' 'palais',
        description "Les dortoirs du palais du roi Mice s'étendent d'est en ouest, tout
            en longueur, et peuvent facilement accueillir plus d'une dizaine des plus
            proches de la couronne. Vous en faites partie, et votre couche est tout au
            fond de la salle, consistant en un amas de pailles de plastique usagées
            liées avec des poils de la terrifiante Choupinette, monstre sanguinaire
            assoiffé de votre sang, qui rode dans les alentours du palais. La salle du
            trône est à l'ouest.",
        scenic_look 'couche' 'couches' 'lit' 'lits' 'couchette' 'couchettes'
            'couchettes' 0 "Toutes plus ou moins identiques à la votre, bien que cette
            dernière ait la particularité d'être nouée à l'aide de poils longs de
            Choupinette, dont la conquête est l'exploit qui à fait votre renommée et
            vous a valu d'être Chevalier du roi.",
        initial [;
          print "^^Vous reviez d'un doux plateau garni de fromages variés, français, suisses et
          hollandais, quand on vient vous réveiller sans ménagement, vous assurant que votre
          bon roi vous demande de toute urgence dans la salle du trône. Quelque peu ronchon
          vous vous levez néanmoins, lissant vos moustaches à gauche, puis à droite, d'un
          geste assuré.^^";
          KeyCharPrimitive();
          ClearScreen();
          rtrue;
        ],
        w_to trou_trone,
  has   scenery light;

Object  heros "vous" trou_dortoirs
  with  etat 0,
        peint 0,
        task0 0,
        task1 0,
        task2 0,
        task3 0,
        capacity 3,
        description [;
          if (foulard hasnt worn && heros.peint == 1 && (papier_toilette hasnt worn ||
            papier_toilette notin player)) {
            "Vous êtes noir de la tête aux pattes grâce à votre plongeon dans le pot de
            peinture noire."; }
          else if (foulard has worn && heros.peint == 0 && (papier_toilette hasnt worn ||
            papier_toilette notin player)) {
            "Ce foulard noir que vous portez noué devant la bouche vous donne un air de
            gangster."; }
          else if (foulard has worn && heros.peint == 1 && (papier_toilette hasnt worn ||
            papier_toilette notin player)) {
            "Votre corps peint associé au foulard noir noué devant votre bouche, vous
            ressemblez vraiment à un gangster ou un agent secret."; }
          else if (papier_toilette has worn && papier_toilette in player && (foulard hasnt
            worn || heros.peint == 0)) {
            "Vous êtes entièrement recouvert de papier toilette et parfaitement ridicule
            mais vous pouvez néanmoins marcher et voir où vous vous déplacez."; }
          else if (papier_toilette has worn && papier_toilette in player && foulard has
            worn && heros.peint == 1) {
            "Vous êtes entièrement recouvert de papier toilette, mais si vous l'enlevez
            vous serez alors entièrement noir, foulard et peinture aidant."; }
          else {
            "C'est bien vous, Chevalier du roi Mice VII, qui servez sous ses ordres
            depuis son ascension au trône puisque vous avez toujours été de grands
            amis."; }
        ],
        each_turn [;
          if (foulard has worn && heros.peint == 0 && (papier_toilette hasnt worn ||
            papier_toilette notin player)) {
            self.etat = 1; }
          else if (foulard hasnt worn && heros.peint == 1 && (papier_toilette hasnt worn ||
            papier_toilette notin player)) {
            self.etat = 2; }
          else if (foulard has worn && heros.peint == 1 && (papier_toilette hasnt worn ||
            papier_toilette notin player)) {
            self.etat = 3; }
          else if (papier_toilette has worn && papier_toilette in player && (foulard hasnt
            worn || heros.peint == 0)) {
            self.etat = 4; }
          else if (papier_toilette has worn && papier_toilette in player && foulard has
            worn && heros.peint == 1) {
            self.etat = 5; }
          else { self.etat = 0; }
        ],
  has   concealed animate proper transparent light;

Object  trou_trone "Palais du roi Mice"
  with  name 'salle' 'palais',
        description "Vous êtes devant le trône de votre roi, Mice VII, dressé dans une
            cavité creusée dans le béton par les pattes de vos ancêtres, sous le vieux
            buffet du salon. Le trône, somptueux, est taillé dans un bloc de
            polystyrène dans lequel deux pieux, ou cure-dents, tiennent lieu
            d'accoudoir. Au dessus du trône se trouve un emplacement circulaire qui
            accueille en principe les armes de la famille royale, mais qui est vide
            aujourd'hui. Le roi, confortablement lové au creux de son siège, promène un
            regard bienveillant sur ses sujets affairés autour de lui, tandis qu'il
            grignotte un morceau de fromage en plastique rigide. De l'entrée en arcade
            du palais provient une faible lumière qui est ingénieusement répartie dans
            la salle à l'aide de bris d'un miroir. La porte est au nord, tandis qu'à
            l'est s'étendent les dortoirs.",
        scenic_look 'trone' 'siege' 'fauteuil' 'chaise' 'somptueux' 'polystyrene'
            'pieux' 'accoudoirs' 'pieu' 'accoudoir' 'cure-dents' 0 "Le trône royal est
            une perle de l'artisanat de la couronne, conçu par l'un de vos plus proches
            amis lorsque le trône séculaire en bouchons de liège s'effrita."
            'fromage' 'plastique' 'rigide' 0 "Un jouet qui a eu le malheur de s'échouer
            à l'entrée du palais alors que le roi tentait une escapade diurne. Son
            Altesse s'est alors prise d'affection pour cette illusion de gruyère qui lui
            rappelle chaque jour les récits de sa mère sur les caves d'une fromagerie
            suisse."
            'miroir' 'bris' 'debris' 'morceaux' 'morceau' 'glace' 'eclat' 'eclats' 0
            "Des éclats d'un beau miroir que les anciens ont récolté durant leurs
            aventures et qui conduisent la lumière de la porte jusqu'au trône qui,
            sans eux, serait plongé dans d'obscures ténèbres.",
        scenic_look2 'arcade' 'entree' 'porte' 0 "Un simple trou dans le mur dont le
            style architectural est des plus classiques : une arcade en demi-ovale
            permettant de faire passer deux sujets de front."
            'embleme' 'armes' 'armoiries' 'sceau' 'blason' 'ecu' 'bouclier' 'symbole' 0
            "L'emplacement qui contient habituellement le blason décoré du gruyère sans
            trous, emblême de la famille royale, est désespérément vide.",
        scenic_touch 'trone' 'siege' 'fauteuil' 'chaise' 'somptueux' 'polystyrene'
            'pieux' 'accoudoirs' 'pieu' 'accoudoir' 'cure-dents' 0 "Poser ne serait-ce
            que l'ombre d'une de vos pattes sur le trône royal vous vaudrait probablement
            une sanction que personne n'a jamais osé imaginer tant la chose paraît
            inconcevable à vos semblables."
            'miroir' 'bris' 'debris' 'morceaux' 'morceau' 'glace' 'eclat'
            'eclats' 0 "Vous parcourez de votre patte le rebord d'un de ces éclats et,
            sans surprise, vous entaillez légèrement la patte, gratifiant d'un léger
            boitement votre démarche.",
        e_to trou_dortoirs,
        n_to buffet_dessous;

Object  buffet_dessous "Sous le vieux buffet"
  with  description "Vous êtes devant l'entrée du palais royal, contre le mur sud du
            salon, sous le vieux buffet qui siège entre l'ordinateur à l'est et le
            téléviseur à l'ouest. Au nord vous êtes au beau milieu de la pièce, à
            découvert, et à la merci de Choupinette.",
        scenic_look 'buffet' 'meuble' 'vieux' 'acajou' 'trou' 0 "Vu du dessous, le
            vieux buffet en acajou paraît impressionnant, surtout à cause de la
            hauteur de plafond dont il vous gratifie qui n'excède pas dix à douze
            centimètres. Dans l'angle sud-ouest se trouve un trou assez large pour s'y
            faufiler, mais il faudrait une perche ou une planchette pour y grimper.",
        s_to trou_trone,
        w_to televiseur,
        e_to ordinateur,
        n_to [; SalonChoupinette(); rtrue; ],
        u_to [;
          if (paille in player || paille in buffet_dessous) {
            move paille to buffet_dessous;
            print "Vous agrandissez la paille rétractable en accordéon et l'installez en
            oblique à travers le trou, puis entreprenez de l'escalader jusqu'en son
            sommet.^";
            PlayerTo(buffet_interieur);
            rtrue;
          }
          else "Vous n'êtes pas assez fort pour sauter jusqu'au trou et vous y hisser
            à la seule force de vos maigres bras.";
        ];

Object  buffet_interieur "Dans le vieux buffet"
  with  name 'vieux' 'buffet' 'meuble',
        description "À l'intérieur du buffet, la seule lumière qui filtre provient
            du trou par lequel vous êtes arrivé et des pourtours des portes de bois.
            Le sol poussiéreux est marqué de traces de pattes de souris vers un angle.
            La sortie est vers le bas.",
        d_to [;
          print "Vous glissez avec précaution le long de la paille.^";
          PlayerTo(buffet_dessous);
          rtrue;
        ];

Object  televiseur "Près du poste de télévision"
  with  name 'poste' 'televiseur' 'tele' 'television' 'tv',
        description "A l'ouest du buffet où se trouve l'entrée du palais royal siège
            fièrement un énorme téléviseur à écran cathodique, qui a longtemps été votre
            terrain de jeu lorsque vous n'étiez qu'un souriceau et que vous pouviez vous
            y faufiler. Aujourd'hui vous êtes trop grand pour y entrer, et vous n'avez
            plus votre insouciance de jeunesse, préférant désormais éviter tout contact
            avec un matériel électrique. Au nord vous pouvez vous glisser derrière le
            canapé installé devant l'écran et rejoindre le balcon, au nord-est vous
            êtes au centre du salon et à l'est vous retournez sous le buffet.",
        scenic_look 'canape' 'sofa' 'divan' 'siege' 'fauteuil' 'chaise' 'banquette' 0
            "Le volumineux sofa de cuir est orienté vers le sud, en face du téléviseur,
            et une jeune fille y est avachie, s'abrutissant au devant d'énormités
            commerciales télévisées, à savoir une publicité pour un fromage de chèvre,
            qui étrangement vous fait envie.",
        e_to buffet_dessous,
        ne_to [; SalonChoupinette(); rtrue; ],
        n_to balcon;

Object  balcon "Sur le balcon"
  with  name 'balcon',
        description "La porte du balcon entrouverte vous permet de vous y faufiler, et
            une légère brise vient caresser vos moustaches frémissantes. Ce lieu semble
            être une annexe du garage pour les propriétaires du lieu tant il est encombré
            de bicylettes, de pots de peinture et balconières garnies de fleurs. Au sud
            vous retrouvez le canapé ainsi que le téléviseur tandis qu'à l'est vous
            traversez le salon à découvert.",
        scenic_look 'bicyclette' 'bicyclettes' 'velo' 'vtt' 0 "Ces engins à roues vous
            fascinent toujours autant, bien que ceux-ci soient manifestement viellis et
            rouillés par des averses fréquentes dans la région."
            'pot' 'pots' 'peinture' 0 "Des pots de peinture noire, bleue et blanche, qui
            ont été utilisés récemment pour refaire la peinture du balcon sur lequel vous
            vous trouvez, et que l'homme de la maison n'a pas encore eu le courage de
            descendre au garage. Divers pinceaux de formes variées encombrent le sol."
            'pinceau' 'pinceaux' 'brosse' 'brosses' 0 "Des pinceaux et brosses dont les
            poils se sont solidifiés avec la peinture qui y a séché.",
        scenic_look2 'balconiere' 'balconieres' 'fleur' 'fleurs' 'tige' 'tiges'
            'feuille' 'feuilles' 0 "Des fleurs de multiples couleurs poussent là dans ces
            bacs spécifiquement prévus pour la culture horticole, surplombant des pots
            de peinture. Un arbuste a même une de ses feuilles qui plonge dans la
            peinture noire tandis qu'une branche descend jusqu'à vos pieds."
            'arbuste' 'branche' 0 "Vous pourriez presque monter jusqu'à cet arbuste en
            grimpant le long de sa branche qui vient mourir à vos pieds.",
        s_to televiseur,
        e_to [; SalonChoupinette(); rtrue; ],
        u_to arbuste,
        before [;
          Climb: PlayerTo(arbuste);
        ];

Object  arbuste "Au sommet de l'arbuste"
  with  name 'arbuste' 'arbre' 'branche' 'branches',
        description "Vous êtes au sommet d'un arbuste qui surplombe un pot de peinture
            noire dans lequel se jette une de ses longues feuilles pendantes, et vous
            pouvez redescendre sur le balcon par une de ses branches qui chemine vers
            le sol.",
        d_to balcon,
        before [;
          Jump,Swim:
            if (heros.peint == 0) {
              print "Pris soudain d'une envie irrépressible, vous bouchez vos narines à
              l'aide d'une de vos pattes et plongez dans le pot de peinture noire. Vous
              en ressortez tout peint en grimpant le long de la feuille, désormais
              invisible dans l'obscurité.^";
              heros.peint = 1;
              rtrue;
            }
            else { "Un seul bain vous suffit."; }
        ];

Object  ordinateur "Sous le bureau de l'ordinateur"
  with  description [;
            print "Vous vous glissez sous le bureau de l'ordinateur familial, un
            énorme engin au ronronnement inquiétant qui résonne de mille "; style
            underline; print "clics"; style roman; " sous l'action effrénée du fils de la
            maison, scotché devant un écran d'une taille démesurée, casque sur les
            oreilles, et gesticulant visiblement aux prises avec des terroristes
            virtuels. A l'ouest vous retrouvez le calme de votre buffet, au nord-ouest
            s'ouvre toujours un espace vide et étrangement silencieux et à l'est se
            trouve l'entrée d'un couloir.";
        ],
        w_to buffet_dessous,
        e_to couloir_salon,
        nw_to [; SalonChoupinette(); ];

Object  couloir_salon "Couloir vers la cuisine"
  with  name 'couloir' 'corridor',
        description "L'unique couloir de la maison qui joint la cuisine au nord aux
            chambres au sud. A l'est se trouvent la salle de bain et les toilettes,
            mais les deux portes sont fermees.",
        w_to ordinateur,
        s_to couloir_chambres,
        n_to cuisine,
        e_to [;
          if (_ordinateur.eteint == 1) { PlayerTo(toilettes); rtrue; }
          else { "La porte est malheureusement fermée."; }
        ];

Object  toilettes "Dans les toilettes"
  with  name 'cuvette' 'trone' 'toilettes',
        description "Vous vous trouvez dans les toilettes de la propriété, attenantes
            à la salle de bain, où le trône prend tout de suite une dimension autre que
            celle du trône du roi Mice VII. À côté de vous se trouve une brosse sur le
            manche de laquelle sont enfilés plusieurs rouleaux de papier toilette blanc
            immaculé. À l'ouest vous retournez dans le couloir.",
        w_to couloir_salon;

Object  couloir_chambres "Couloir vers les chambres"
  with  name 'couloir' 'corridor' 'porte',
        description "Le couloir qui joint la cuisine aux chambres, du nord au sud, donne
            ici sur trois chambres : celle du fils à l'ouest, de la fille au sud et des
            parents à l'est, cette dernière étant la seule porte ouverte par ailleurs.",
        w_to "La porte est fermée.",
        e_to chambre_parents,
        s_to "La porte est fermée.",
        n_to couloir_salon;

Object  chambre_parents "Chambre des parents"
  with  name 'chambre' 'porte' 'habitation',
        description "La chambre des propriétaires de la maison est des plus sommaires,
            contenant un lit deux places, un bureau, une commode et une armoire
            entrouverte. Une fenêtre, fermée, donne sur le jardin et la mère y est
            accoudée, épuisant une cigarette.",
        scenic_look 'meuble' 'meubles' 'lit' 'bureau' 'commode' 'armoire' 0 "Les meubles
            de cette pièces sont tous en noyer, et ont sûrement été achetés au même
            endroit et en même temps."
            'cigarette' 'clope' 'cigare' 'petard' 0 "Une cigarette sans filtre, banale."
            'fenetre' 'carreau' 0 "Une fenêtre a carreaux, comme dans le reste de la
            maison.",
        count 0,
        each_turn [;
          self.count = self.count + 1;
          if (self.count == 3) {
            print "^Un cri de stupeur retentit lorsque la femme se détourne de la
            fenêtre, sa cigarette consummée, et vous comprenez que vous en êtes le
            motif. Affolée, elle court vers le couloir et vous écrase au passage.";
            deadflag = 4;
          }
        ],
        w_to couloir_chambres;

Object  cuisine "La cuisine"
  with  description [;
            print "La cuisine, relativement exiguë, est relativement calme, les enfants
            étant plongés dans leurs activités numériques tandis que la mère se trouve
            dans sa chambre. Du robinet au dessus de l'évier goutte à intervalle
            régulier une petite note d'eau qui vient résonner de son "; style underline;
            print "ploc"; style roman; " dans l'atmosphère moite de la pièce. Au sud se
            trouve le couloir menant au salon et aux chambres, tandis qu'à l'ouest un
            escalier descend vers la cave et qu'à l'est se tient la porte d'entrée de la
            maison.";
        ],
        scenic_look 'evier' 'robinet' 'lavabo' 'goutte' 'eau' 0 "Matériel désormais
            déprécié, au profit du lave-vaisselle, l'évier trône fièrement avec comme
            seule ambition les plats trop grands et la poubelle qu'il couve.",
        count 0,
        each_turn [;
          self.count = self.count + 1;
          if (self.count == 5) {
            move pere to chambre_parents;
            "^La porte s'ouvre brusquement sur le maître de maison, rentrant du jardin
            où il cultivait son potager. Il se dirige vers la cave, ouvrant la porte,
            s'y déleste de son matériel de jardinage et pars retrouver sa femme, laissant
            la porte grande ouverte à l'ouest.";
          }
        ],
        s_to couloir_salon,
        e_to "Sortir de ce lieu signifierait probablement la mort, et si vous surviviez,
            le reniement de toutes les valeurs monarchiques en lesquelles vous croyez et
            une injure à votre roi.",
        w_to [;
          if (self.count >= 5) { PlayerTo(cave); rtrue; }
          else { "La porte est malheureusement fermée."; }
        ];

Object  cave "Au plus profond de la cave"
  with  description "La cave de la maison est extrêmement petite, et ne sert qu'à
            entreposer le vin et les outils et vêtements de jardin du père. L'obscurité
            est quasi totale puisque seul un soupirail vient éclairer le lieu, mais vous
            repérez néanmoins l'entrée du repaire de Kay sur le mur nord. L'escalier
            remonte à la cuisine à l'est.",
        scenic_look 'soupirail' 'lumiere' 'fenetre' 0 "Une portion de cercle en fonte
            réhaussée de verre gris de crasse, on peut appeler ça un soupirail, avec de
            l'imagination."
            'vin' 'bouteille' 'bouteilles' 'pinard' 'alcool' 'boisson' 'outils' 'vetements'
            'jardin' 'pantalon' 'pelle' 'cotes' 'rhone' 'caisse' 'caisses' 'etabli' 0
            "Des bouteilles de Côtes du Rhône bon marché sont stockées là dans des caisses,
            jouxtant un établi sur lequel reposent un vieux pantalon et divers outils de
            jardinage, dont une pelle à ce que vous parvenez à en voir depuis le sol.",
        passe 0,
        n_to [;
          if (heros.task1 == 1) {
            "Retournez chez Kay maintenant serait du suicide. Mieux vaut se hâter d'aller
            voir le roi.";
          }
          if ((heros.etat == 4 || heros.etat == 5) && self.passe == 0) {
            self.passe = 1;
            print "Gauche et veule, vous avancez néanmoins au devant des gardes du trou
            qui sont saisis d'effroi à la vue de votre accoutrement.^^
            ~Un fantôme ! Un fantôme !~ s'écrie le premier, terrifié, tandis que le
            second s'enfuit en vous frôlant.^^
            Amusé, vous pouvez désormais pénétrer dans l'enceinte du repaire du gang du
            rat Kay.^";
            Achieved(0);
            heros.task0 = 1;
            PlayerTo(kay_hall);
            rtrue;
          }
          if (heros.etat == 3 && self.passe == 0) {
            self.passe = 1;
            print "Avançant précautionneusement dans l'ombre, vous vous faufilez
            entre les gardes et pénétrez dans l'enceinte du repaire du gang du rat
            Kay, grâce à votre apparence complètement noire qui vous rend invisible dans
            l'ombre.^";
            Achieved(0);
            heros.task0 = 1;
            PlayerTo(kay_hall);
            rtrue;
          }
          if ((heros.etat == 4 || heros.etat == 5) && self.passe == 1) {
            print "Vous faites une nouvelle apparition fantasque dans votre accoutrement et
            manquez de tuer le garde en l'effrayant à nouveau.^";
            PlayerTo(kay_hall);
            rtrue;
          }
          if (heros.etat == 3 && self.passe == 1) {
            if (lampe in player && lampe has light) {
              print "La lumière de votre lampe vous trahit.";
              deadflag = 5; rtrue;
            }
            else {
              print "Toujours aussi discret, vous vous faufilez à l'extérieur.^";
              PlayerTo(kay_hall);
              rtrue;
            }
          }
          if (heros.etat == 2) {
            if (lampe in player && lampe has light) {
              print "La lumière de votre lampe vous trahit.";
              deadflag = 5; rtrue;
            }
            else {
              print "Vous parvenez à vous faufiler dans l'ombre grâce à votre teinture
              nouvellement noire mais vos dents blanches captivent un rayon lumineux et
              vous font repérer.";
              deadflag = 5;
              rtrue;
            }
          }
          if (heros.etat == 1) {
            if (lampe in player && lampe has light) {
              print "La lumière de votre lampe vous trahit.";
              deadflag = 5; rtrue;
            }
            else {
              print "Malgré votre allure de gangster, votre pelage gris clair ne trompe pas
              et les gardes s'emparent de vous sans cérémonie.";
              deadflag = 5;
              rtrue;
            }
          }
          else {
            print "Vous tentez de glisser une patte dans le trou sombre qui fait
            office d'entrée au repaire. D'un geste un garde vous attrape et vous fait
            prisonnier.";
            deadflag = 5;
            rtrue;
          }
        ],
        e_to cuisine;

Object  kay_hall "Hall du repaire de Kay"
  with  description "L'entrée de l'antre de Kay s'enfonce de plusieurs centimètres et
            passe sous le mur de la maison. Les relents de rats crevés qui parviennent
            jusqu'à vous vous donnent d'imaginer ce qu'il pourrait advenir de vous s'il
            vous mettaient la patte dessus. Sur les murs sont installées des diodes de
            récupération branchées à une dynamo au moins grosse comme vous qu'actionne un
            jeune rat difforme, et aveugle semble-t-il. Mais loin d'être pessimiste vous
            préférez analyser froidement la situation : selon les pancartes, à l'ouest
            seraient les quartiers des membres du gang, à l'est les prisons et l'entrepôt,
            et au nord la salle de réunion où se tient probablement le rat Kay lui-même. Au
            sud, la lumière, et la sortie.",
        scenic_look 'pancarte' 'panneau' 'bois' 'miteux' 0 "Trois pancartes de bois miteux
            sur les murs nord, est et ouest."
            'velo' 'dynamo' 'vieux' 'rat' 'aveugle' 0 "Un moment il vous semble que le
            vieux rat vous contemple... Peut-être un aveugle a-t-il plus de facilités à
            vous repérer dans l'obscurité qu'un autre rat. Mais il détourne son regard
            torve et inexpressif."
            'diodes' 'diode' 'lampe' 'lampes' 'lumiere' 'lumieres' 0 "Des diodes
            électroluminescentes rouges et vertes, qui pulsent parfois sous l'action
            irrégulière du vieux rat.",
        before [;
          Smell: "Plusieurs rats en décomposition doivent se trouver non loin, vu l'odeur
            nauséabonde qui règne en ce lieux.";
          Listen: "Seuls le crépitement de diodes usagées et le grincement du vélo sur
            lequel pédale le vieux rat chargé de produire l'électricité viennent perturber
            le silence de mort qui règne ici.";
        ],
        s_to [;
          if (heros.etat == 4 || heros.etat == 5) {
            print "Vous faites une nouvelle apparition fantasque dans votre accoutrement et
            manquez de tuer le garde en l'effrayant à nouveau.^";
            PlayerTo(cave);
            rtrue;
          }
          if (heros.etat == 3) {
            if (lampe in player && lampe has light) {
              print "La lumière de votre lampe vous trahit.";
              deadflag = 5;
            }
            else {
              print "Toujours aussi discret, vous vous faufilez à l'extérieur.^";
              PlayerTo(cave);
              rtrue;
            }
          }
        ],
        n_to [;
          if (entrepot has visited) return couloir_reunion;
          else "Il est possible que le blason soit simplement dans l'entrepot a l'est,
              autant ne pas prendre de risques inutiles.";
        ],
        e_to couloir_prisons,
        w_to couloir_dortoirs;

Object  couloir_prisons "Couloir des prisons"
  with  name 'prison' 'couloir' 'corridor' 'prisons' 'geole' 'geoles' 'trou' 'taule'
            'cellule',
        description "Au nord, au sud, quatre prisons, dans deux desquelles se morfondent
            deux rats, l'un blanc, sûrement isolé car il était albinos, l'autre plus
            classique, probablement un opposant politique ou un traître. Toujours est-il
            qu'aucun n'ouvre la bouche. Le couloir chemine d'est en ouest, vers le hall à
            l'ouest et l'entrepôt à l'est.",
        scenic_look 'albinos' 'rat' 'opposant' 'politique' 'traitre' 'blanc' 'isole' 0
            "Les deux prisonniers observent un silence macabre et semble résignés,
            peut-être sont-ils là depuis longtemps... Vous frissonnez à l'idée de les
            rejoindre.",
        w_to kay_hall,
        e_to couloir_entrepots,
        s_to "Les deux portes sont évidemment verrouillées.",
        n_to [;
          print "L'une des portes est ouverte et donne sur une geôle vide, qui semble
          prévue à votre attention.";
          return prison;
        ];

Object  prison "Dans une geôle vide"
  with  name 'prison' 'geole' 'geoles' 'trou' 'taule' 'oubliette' 'cellule' 'mur' 'murs'
            'griffes',
        description "Des murs griffés par les générations de prisonniers qui s'y sont
            succédés, cette geôle vous apparaît lugubre, dans sa sinistre robe grisâtre.
            Des barreaux verticaux, serrés et raides, forme la façade sud dans laquelle
            s'ouvre la porte, de barreaux elle aussi, par laquelle vous êtes entré.",
        temps 0,
        scenic_look 'porte' 'barreaux' 'barreau' 'facade' 0 "Ces barreaux sont faits de
            piques à brochette de métal solidement ancrés dans le béton.",
        initial [;
            "Vous avez la désagréable impression que ce lieu sera pour longtemps votre
            résidence et songez à sortir.";
        ],
        each_turn [;
          self.temps++;
          if (self.temps == 5) {
            print "C'est alors qu'un rat passant par hasard dans le couloir vous apperçoit
            dans la lumière d'une diode et aussitôt, referme la porte de la cellule.";
            deadflag = 5;
          }
        ],
        s_to couloir_prisons;

Object  couloir_entrepots "Couloir de l'entrepôt"
  with  name 'couloir' 'corridor' 'entrepot' 'entrepots',
        description "Vous avancez dans le corridor qui mène à l'entrepôt principal, là où
            les rats entreposent la plupart de leurs conquêtes. La porte de l'entrepôt se
            trouve à l'est, tandis qu'à l'ouest vous retournez vers les prisons. Mais la
            majeure difficulté réside dans le garde endormi posté devant la porte.",
        w_to couloir_prisons,
        e_to porte_entrepot;

Object  entrepot "Entrepôt du gang de Kay"
  with  name 'entrepot' 'balles' 'luminescentes' 'lumineuses' 'balle' 'luminescente'
            'lumineuse' 'entrepots',
        description "Circulant au milieu de petites boîtes en carton et de balles
            luminescentes qui procurent la faible lumière de ce lieu, vous cherchez
            désespérément trace du blason de votre roi, en vain. Dans les boîtes, des
            vivres en abondance, du pain rassis principalement, et parfois quelque fromage
            moisissant dont vous humez les fragrances avec délectation. Dans un coin,
            un matelas de paille et un verre renversé en guise de table, probablement le
            logis du gardien.",
        scenic_look 'lit' 'matelas' 'paille' 'couche' 'couchette' 'logis' 0 "De la vraie
            paille naturelle, pas comme vos restes de petit déjeuner, est agglutinée en un
            matelas qui semble plutôt confortable."
            'verre' 'renverse' 'table' 'tablette' 0 "Une table originale sur laquelle
            trainent, là aussi, de vieux morceaux de fromage.",
        before [;
          Smell:
            "Les délicieuses fragrances de mets mélangés viennent chatouiller vos
            narines.";
        ],
        w_to porte_entrepot;

Object  couloir_dortoirs "Couloir des dortoirs"
  with  name 'couloir' 'corridor' 'dortoir' 'dortoirs' 'quartiers' 'membres' 'gang',
        description "Des bannières pleines de sang séché pendent à des tringles rouillées,
            marquant l'entrée des quartiers des membres du gang de Kay. Au nord et au sud,
            deux passages mènent à deux dortoirs. À l'est, vous retournez vers le hall.",
        scenic_look 'banniere' 'bannieres' 'pleines' 'sang' 'seche' 'drapeau' 'etendard'
            'ecussons' 'pseudo' 'pseudo-etendard' 'pseudo-etendards' 'etendards' 'tringle'
            'tringles' 'rouillee' 'rouillees' 0 "Ces pseudo-étendards sont en fait des
            écussons scouts de la fille des propriétaires du lieu, devenus inutiles
            lorsque le téléviseur prit la place de ses instincts de guide. Tachés de sang,
            vous n'osez imaginer l'usage que l'on a pu en faire.",
        initial [;
          if (_poignard.trouve == 1) {
            style underline; print "Stanley..."; style roman; print "Qu'est-ce que Stanley
            avait à voir avec cette intrigue ? Pourquoi est-ce que son poignard se trouve
            dans le repaire de Kay ?^^
            Stanley est un de vos amis d'enfance. Le roi actuel, lui et vous formiez un
            trio inséparable, mais il y a peu Stanley a disparu sans laisser de nouvelles.
            Serait-ce possible que... que Stanley soit véritablement le voleur ?^^";
          }
        ],
        before [;
          Smell: "Le sang et la sueur...";
          Listen:
            print "Les éclats de rire gras se mêlent au thème de la "; style underline;
            print "Lettre à Élise"; style roman; print " et du choc des dés sur la table,
            au nord."; rtrue;
        ],
        n_to [;
          print "Vous jetez un oeil du côté du dortoir nord et repérez trois soldats en
          train de jouer aux dés en riant grassement et en écoutant la "; style underline;
          print "Lettre à Élise"; style roman; print " sur une vieille autoradio qui prend
          la moitié de la salle à elle seule. La lumière y abonde et vous doutez que votre
          déguisement y soit du meilleur effet, de plus, un cure-dent métallique taillé en
          pointe reluit au côté de l'un des rats."; rtrue;
        ],
        e_to kay_hall,
        s_to kay_dortoir;

Object  kay_dortoir "Dortoir sud"
  with  name 'dortoir' 'quartiers' 'membres' 'gang' 'sud',
        description "D'une configuration semblable au dortoir du nord, celui-ci semble plus
            austère, et un calme plat y règne. La lumière y est aussi importante, mais
            personne ne semble là pour vous y identifier. Des lits superposés taillés dans
            de vieux tiroirs de bois sont alignés sur deux rangées, et quelques boîtes
            d'allumettes viennent jouer le rôle de coffres personnels.",
        scenic_look 'lit' 'lits' 'superpose' 'superposes' 'tiroir' 'tiroirs' 0 "D'un bois
            peu solide, on sent néanmoins que les rats ont le sens de la récupération plus
            fin que les souris qui se content de polystyrène quand elles pourraient ronger
            du hêtre."
            'coffre' 'personnel' 'coffres' 'personnels' 'cassette' 'caisse' 'malle' 'boite'
            'boites' 'allumette' 'allumettes' 0 "Une boîte de 240 allumettes attire votre
            attention car elle est entrouverte et un objet fin y reluit. En vous
            approchant vous découvrez un fin poignard d'acier trempé dont la garde dépasse
            de la boîte.",
        n_to couloir_dortoirs;

Object  couloir_reunion "Couloir vers la salle de réunion"
  with  name 'couloir' 'corridor' 'reunion' 'salle' 'hall',
        description "Un long couloir conduit au nord à la salle de réunion, tandis que
            s'ouvrent à l'ouest les appartements personnels du rat Kay. Un jeune rat
            grassouillet garde la porte au nord, arborant un sourire béat et doté d'un air
            idiot hors du commun.",
        s_to kay_hall,
        w_to kay_chambre,
        n_to [;
          if (salle_reunion has visited) {
            print "~Tu veux d'jà r'passer ? D'accord...~";
            PlayerTo(salle_reunion);
            rtrue;
          }
          else { "~Tu passes pas, c'est m'sieur Kay qui me l'a dit.~"; }
        ];

Object  kay_chambre "Chambre de Kay"
  with  name 'kay' 'chambre' 'quartier' 'appartement' 'maison' 'chef' 'gang',
        description "La chambre de Kay, chef du gang de rats mal intentionnés qui sévit
            dans la maison, volant nourriture, biens et même parfois jeunes femelles, est
            à l'image du personnage : les murs de terre sèche, car en creusant les rats
            sont sortis des fondations de la maison, sont décorés de traces de pattes à la
            peinture rouge, ou au sang peut-être, mais mieux vaut ne pas y songer. Des
            crânes d'oisillons tombés du nid trônent sur une boîte à musique reconvertie
            en bureau, qui joue un hymne effrayant si l'on en tourne la manivelle.^
            Une lampe de vélo fait ici office de lumière d'éclairage, de forme circulaire
            et relativement plate, qui s'allume en y excerçant une impulsion.^
            Le lit en est un vrai : un jouet de fillette initialement conçu pour une poupée
            sur lequel chaque rat du gang semble avoir écrit un message incongru ou
            dessiné quelque obscénité. Le bon goût de Kay n'est plus à remettre en
            question. La sortie est à l'est.",
        scenic_look 'murs' 'mur' 'terre' 'seche' 'traces' 'trace' 'pattes' 'patte'
            'peinture' 'rouge' 'sang' 0 "Ces peintures donnent un aspect tribal à la
            chambre de Kay."
            'lit' 'jouet' 'fillette' 'poupee' 'message' 'incongru' 'dessin' 'obscenite'
            'obscene' 0 "Le gratin des philosophes rats s'exprime ici en mots pesés et
            choisis qu'il serait déplacé de détailler, étant des propos qui dépassent notre
            conception de l'intelligence pour rejoindre les sphères éthérées du génie :
            la vulgarité dans toute sa splendeur, pour parler clairement.",
        e_to couloir_reunion;

Object  salle_reunion "Antichambre de la salle de réunion"
  with  description "Vous vous trouvez dans une petite antichambre juste avant la salle de
            réunion au nord où se trouve probablement Kay actuellement. La hall se trouve
            au sud.",
        vu 0,
        time_left,
        time_out [;
          if (location == cave) { StopTimer(self); }
          else {
            print "^^Haletant, Kay vous ratrappe avec une joie sadique non feinte.";
            deadflag = 5;
          }
        ],
        s_to couloir_reunion,
        n_to [;
          if (self.vu == 0) {
            self.vu = 1;
            "Vous jetez un oeil en entrouvrant la porte au nord, et vous voyez Kay et
            deux acolytes en train de discuter autour d'une table en plastique jaune. De
            nombreux éclats de miroir constellent les parois de la salle et lui donnent une
            allure étrange. Le blason est au milieu d'eux, sur la table.^^
            Vous revenez dans l'antichambre afin de vous assurer une dernière fois que vous
            avez une idée de comment vous emparer du blason.^";
          }
          else if (self.vu == 1) {
            if (lampe in player && lampe has light) {
              if (foulard has worn) {
                print "La lampe allumée à la main, vous faites brusquement irruption dans
                la salle de réunion. Jouant des reflets sur les miroirs tout en vous
                protégeant avec le foulard, vous éblouissez les trois rats et tandis
                qu'ils se couvrent les yeux de leurs pattes poisseuses, vous vous ruez et
                subtilisez le blason du roi.^^À peine sorti de la salle, vous entendez les
                pas lourds de Kay qui vous poursuit, et vous vous dites qu'il vaudrait
                mieux ne pas traîner.^";
                move blason to hotte;
                heros.task1 = 1;
                score = score + 5;
                Achieved(1);
                StartTimer(self,3);
                rtrue;
              }
              else {
                print "La lampe allumée à la main, vous faites brusquement irruption dans
                la salle de réunion. Jouant des reflets sur les miroirs, vous vous
                éblouissez vous même autant que les trois rats et chancellez, si bien que
                Kay pose sa main sur votre épaule...^";
                deadflag = 5; rtrue;
              }
            }
            else if (heros.etat == 4 || heros.etat == 5) {
              print "Vous pénétrez tout pataud dans la salle de réunion tandis que les
              trois rats vous fixent, pétrifiés par la stupeur. Tant et si bien que vous
              parvenez à avancer jusqu'à eux, à saisir le tableau, et à faire demi-tour
              jusqu'à l'antichambre quand Kay sort de sa torpeur :^^
              ~Quel est ce type ridicule recouvert de papier toilette ?~^^
              Et ne trouvant aucun écho à sa question, Kay se lance à votre poursuite.^";
              move blason to hotte;
              heros.task1 = 1;
              Achieved(1);
              StartTimer(self,3);
              rtrue;
            }
            else {
              print "Vous faites trois pas dans la salle de réunion, et êtes démasqué à la
              lumière blâfarde du lieu.";
              deadflag = 5; rtrue;
            }
          }
        ];

Object  blason "blason de la famille royale"
  with  name 'blason' 'ecu' 'bouclier' 'armes' 'armoiries' 'ecusson' 'heraldique' 'royal'
            'royale',
        article "le",
        description "L'objet qui vous aura vallu cette quête dont vous vous seriez bien
            passé. En son centre est gravé un gruyère sans trous.",
        before [;
          Drop,ThrowAt,ThrowDir:
            "Maintenant que vous l'avez, vous ne le lâchez plus, sauf au roi.";
        ];

!=======================================================================================

Object  hotte "hotte en plastique jaune" trou_dortoirs
  with  name 'hotte' 'sac' 'dos' 'conteneur',
        article "une",
        description "Un jouet sur lequel vous êtes tombé dans l'une de vos escapades,
            probablement associé jadis à une figurine de père Noël, toujours est-il que
            sa forme est parfaitement adaptée aux objets que vous pourriez transporter.",
        capacity 3,
  has   container open openable female;

Object  _paille "paille de plastique" trou_dortoirs
  with  name 'paille' 'pailles',
        description "Les pailles de plastique jonchent le sol de la cuisine tous les
            matins, et vos amis n'ont pas eu beaucoup de mal à s'en approprier
            suffisament pour confectionner ces couchettes.",
        prise 0,
        before [;
          Touch,Take:
            if (self.prise == 0) {
              self.prise = 1;
              move paille to player;
              "Vous sélectionnez délicatement une des pailles les plus rigides dans une
              couchette (pas dans la votre bien évidemment, vous tenez à votre confort),
              et vous en emparez d'un geste.";
            }
            else {
              "Vous n'êtes pas vicieux au point de dénaturer les couchettes de tous vos
              congénères, aussi préférez-vous vous en tenir à une seule paille.";
            }
        ],
  has   scenery female;

Object  paille "paille de plastique"
  with  name 'paille' 'pailles',
        description "Les pailles de plastique jonchent le sol de la cuisine tous les
            matins, et vos amis n'ont pas eu beaucoup de mal à s'en approprier
            suffisament pour confectionner ces couchettes.",
        before [;
          Touch,Take:
            if (buffet_interieur has visited) {
              "Vous n'en aurez sûrement plus intérêt, autant la laisser là pour de
              futures intrusions dans le buffet.";
            }
            else { rfalse; }
         ],
  has   female;

Object  _sujets "sujets de la cour" trou_trone
  with  name 'sujet' 'sujets' 'souris' 'rat' 'rats' 'gens' 'ouvriers' 'travailleurs'
            'citoyens' 'citoyennes' 'citoyen' 'citoyenne' 'courtisan' 'courtisans'
            'courtisane' 'courtisanes' 'cour',
        description "Vos congénères sont ici affairés à converser gaiement, à feindre de
            chercher l'écu royal, ou pour les plus sérieux à remettre en place les
            miroirs, qui doivent exprimer une disymétrie si parfaite, pour la
            satisfaction du roi, qu'elle en est presque mathématique.",
  has   scenery animate pluralname;

Object  roi "roi Mice VII" trou_trone
  with  name 'roi' 'sire' 'mice' 'vii' 'seigneur' 'suzerain' 'souverain' 'monarque',
        description "Le roi Mice VII est la plus énorme des souris que vous ayez eu
            l'occasion de rencontrer, sa bedaine frottant régulièrement le polystyrène
            vieilli du trône en un crissement désagréable. Ses deux grosses pattes avant
            reposent comme mortes sur les accoudoirs en cure-dents plantés directement
            dans le dossier du siège. Les moustaches de votre suzerain s'étendent,
            roides et horizontales, sur plusieurs centimètres, si bien que quand le roi
            ne passe pas une porte du fait de sa corpulence, ses moustaches l'en
            avertissent avant d'être coincé dans l'embrasure.",
        quete_donnee 0,
        initial [;
          if (self.quete_donnee == 0) {
            self.quete_donnee = 1;
            move parchemin to player;
            "~Te voilà enfin, mon cher Chevalier. Je t'ai fait quérir car je dois te
            faire part d'une requête importante...~^^
            Le roi s'arrête un instant, refoulant un éternuement, et poursuit son
            discours, non sans bégayer.^^
            ~Peut-être l'as-tu remarqué, le blason de la famille royale nous a été volé.
            La bande de Kay, les rats qui vivent dans la cave de la maison, a déjà
            revendiqué le crime, bien que j'aie toujours du mal à concevoir comment ils
            ont procédé, étant trop grands pour pénétrer notre palais.^
            Toujours est-il que...~^^
            Le roi s'interrompt à nouveau, toussant violemment si bien que vous n'avez
            d'autre recours que d'aller lui tapoter gentillement le dos de votre patte
            agile jusqu'à ce qu'il retrouve une respiration correcte.^^
            ~Hu... Excuse-moi... Bref, je disais que j'ai besoin de toi pour récupérer
            cet artefact de valeur inestimable, rongé dans une plaque de PVC par mon
            arrière grand oncle, et gravé par sa soeur et deux jeunes vierges.^
            Voici tes ordres de mission. J'ai confiance en toi, mon meilleur Chevalier.
            Je sais que tu ne me décevras pas. Maintenant, vas !";
          }
          if (heros.task0 == 1 && heros.task1 == 1) {
            if (heros.task3 == 1) {
              print "À votre entrée dans la salle du trône, tous les regards se tournent
              sur vous et le blason que vous portez au bras. Les chuchotements vont bon
              train et peu à peu une clameur s'élève, jusqu'à ce que le roi se rende
              compte de la situation.^^
              ~Ahem...~ commence-t-il, et tous les badauds se taisent et l'observent.
              ~Mon cher ami, je crois voir que ta mission a été un succès...~^^
              Vous hochez de la tête et avancez vers le trône.^^
              ~... et je t'en félicite, tout comme je m'en félicite de t'avoir désigné.
              As-tu cependant trouvé quel était le voleur ?~^^
              ~Sire...~, hésitez-vous, ~Il n'est autre que Stanley...~^^
              ~Stan ?~ rugit le roi. ~Le traître... Nous en parlerons en privé mon ami.
              Pour l'heure, festoyons, mes neveux viennent de me rapporter du gruyère";
              style underline; print " sans trous "; style roman; print "qu'ils ont
              trouvé ce matin.~^^
              Vous tendez le blason au roi qui le réaccroche au dessus de son trône, et
              les réjouissances commencent.";
              heros.task2 == 1;
              Achieved(2);
              deadflag = 2;
            }
            else {
              print "À votre entrée dans la salle du trône, tous les regards se tournent
              sur vous et le blason que vous portez au bras. Les chuchotements vont bon
              train et peu à peu une clameur s'élève, jusqu'à ce que le roi se rende
              compte de la situation.^^
              ~Ahem...~ commence-t-il, et tous les badauds se taisent et l'observent.
              ~Mon cher ami, je crois voir que ta mission a été un succès...~^^
              Vous hochez de la tête et avancez vers le trône.^^
              ~... et je t'en félicite, tout comme je m'en félicite de t'avoir désigné.
              As-tu cependant trouvé quel était le voleur ?~^^
              Vous vous confondez en excuses de n'avoir pu trouver de preuves.^^
              ~Et bien, je suppose que ce n'est pas bien grave.^
              Pour l'heure, festoyons, mes neveux viennent de me rapporter du gruyère";
              style underline; print " sans trous "; style roman; print "qu'ils ont
              trouvé ce matin.~^^
              Vous tendez le blason au roi qui le réaccroche au dessus de son trône, et
              les réjouissances commencent.";
              heros.task2 == 1;
              Achieved(2);
              deadflag = 2;
            }
          }
        ],
  has   animate male proper;

Object  parchemin "parchemin royal"
  with  name 'parchemin' 'royal' 'ordre' 'ordres' 'mission' 'document',
        description [;
            style underline;
            print "^Jour 23 de l'année du Reblochon.^^
            Les objectifs de mission suivants sont à atteindre pour le Chevalier porteur
            du présent ordre de mission royal, apposé du sceau royal :^
            1) Se frayer un chemin jusqu'à la cave et s'introduire dans le repaire du
            rat Kay et de son gang.^
            2) Récupérer le blason de la famille royale et donc du roi Mice VII.^
            3) Ramener l'artefact au palais royal et le remettre au roi.^
            X) Objectif facultatif : identifier le voleur.^^";
            style roman;
            "Le document est signé de la main du roi et son sceau y est apposé,
            représentant un gruyère sans trous.";
        ];

Object  foulard "foulard noir" buffet_interieur
  with  name 'foulard' 'noir' 'cagoule' 'bandit',
        description "Un foulard noir qui donne un vrai air de bandit au plus candide des
            souriceaux en recouvrant le museau.",
        before [;
          Disrobe:
            if (heros.etat == 3 && cave.passe == 1) {
              "Il serait fou de se mettre à découvert maintenant, mieux vaut rester
              invisible dans l'ombre.";
            }
            if (heros.etat == 5 && cave.passe == 1) {
              "Ne vaudrait-il pas mieux enlever cet accoutrement ridicule de fantôme et
              rester invisible dans l'ombre ?";
            }
        ],
  has   clothing;

Object  fille "fille" televiseur
  with  name 'fille' 'jeune' 'femme' 'avachie' 'gamine' 'enfant' 'mome' 'demoiselle'
            'loque',
        article "la",
        description "La demoiselle que vous avez l'occasion de contempler dans toute sa
            magnificence de loque avachie est la fille des propriétaires du lieu, qui
            semble vous affectionner et dépose parfois quelques pop-corn devant le sofa,
            que vos semblables se font une joie de récupérer pour festoyer au
            palais.",
        before [;
          Smell: "Un trop plein de parfum vient embrumer votre esprit. La jeune
            demoiselle pourrait avoir un certain charme si elle éviter de vider son
            flacon dans son cou, pensez-vous.";
          Listen: "Un silence captivé, ponctué parfois de ~Non, Stecy ne quitte pas
            Brandon, sinon il te trompera avec Vanessa !~";
        ],
  has   animate female;

Object  _ordinateur "ordinateur" ordinateur
  with  name 'ordinateur' 'ordi' 'bureau' 'computer' 'pc' 'machine' 'fixe' 'engin'
            'bouton' 'power' 'reset',
        description "L'énorme machine et son ronronnement monstrueux vous rappellent
            que vous avez failli mourir jadis étouffé par la poussière qui le garnit,
            d'une épaisseur résultant d'un amoncellement séculaire.",
        eteint 0,
        before [;
          Touch,SwitchOff:
            if (self.eteint == 0) {
              print "Vous promenez une patte discrète sur les cablages de la machine
              et suite à une maladresse, débranchez un des fils. Un hurlement de rage
              retentit et le jeune garçon se lève d'un bond, proférant jurons et
              injures. Il lui faut plusieurs minutes pour découvrir la source de la
              coupure, et vous avez l'occasion de voir son visage bouffi s'empourprer
              quand son sang lui monte au cerveau alors qu'il est penché pour
              rebrancher le cable, et ressortir grisâtre, poussiéreux.";
              self.eteint = 1;
              "^^Forcé de s'arrêter par le rédémarrage de l'ordinateur, le jeune homme en
              profite pour s'éclipser aux toilettes, dont il laisse la porte
              entrouverte en sortant.";
            }
            else { "Il ne faut pas abuser des bonnes choses."; }
          Smell: "Une inspiration et vous éternuez, il vous faudra retenir votre
            respiration pour passer.";
        ],
  has   scenery;

Object  fils "fils" ordinateur
  with  name 'homme' 'jeune' 'garcon' 'fils' 'enfant' 'gamin' 'mome' 'geek' 'nerd'
            'accro' 'no-life',
        article "le",
        description "Le jeune no-life, mal rasé et cheveux gras, passe le plus clair de
            son temps devant l'engin effroyable, du moins vous ne l'avez jamais vu s'en
            éloigner, à tuer virtuellement d'autres accros à distance.",
        before [;
          Smell: "Des relents de sueur mêlés à diverses odeurs nauséabondes, un
            adolescent, un vrai !";
          Listen: "~Gn... Prends-ça ! Headshot... Yes ! Rah le noob... Ghh...~^^
            Force est d'avouer que sa conversation est passionnante.";
        ],
  has   animate male;

Object  _papier_toilette "feuilles de papier toilette" toilettes
  with  name 'papier' 'toilette' 'cul' 'hygiénique' 'rouleau' 'rouleaux' 'feuille'
            'feuilles' 'blanc' 'pq' 'brosse' 'manche',
        description "Des feuilles à double épaisseur, vous pourriez en emporter pour
            améliorer votre couchette au dortoir.",
        before [;
          Touch,Take,Push,Pull:
            move papier_toilette to player;
            "Vous poussez de votre patte l'un des rouleaux, ce qui à pour effet de faire
            balloter le manche de la brosse tant et si bien que le rouleau situé tout en
            haut finit par vous tomber dessus, et en vous en extirpant vous vous
            empêtrez dans les feuilles de papier qui vous recouvrent presque
            entièrement.";
        ],
  has   scenery pluralname female;

Object  papier_toilette "feuilles de papier toilette"
  with  name 'papier' 'toilette' 'cul' 'hygiénique' 'rouleau' 'rouleaux' 'feuille'
            'feuilles' 'blanc' 'pq',
        description "Des feuilles à double épaisseur, vous pourriez en emporter pour
            améliorer votre couchette au dortoir.",
        before [;
          Disrobe:
            if (heros.etat == 4 && cave.passe == 1) {
              "Il ne serait pas prudent de vous rendre reconnaissable maintenant. Certes
              vous n'êtes pas discret, mais avec de la chance, vous ferez sur ceux qui
              vous verront le même effet que sur les gardes.";
            }
            if (heros.etat == 5 && cave.passe == 1) {
              print "Il est vrai qu'il est plus intelligent de rester discret, dans votre
              déguisement noir, que de vous balader couvert de papier toilette.^";
              rfalse;
            }
            else { rfalse; }
        ],
  has   clothing worn pluralname female;

Object  mere "mère" chambre_parents
  with  name 'femme' 'mere' 'proprietaire' 'dame' 'madame' 'fille' 'fumeuse',
        article "la",
        description "Tirant sur sa cigarette sans filtre à intervalles réguliers, cette
            femme d'une quarantaine d'années semble plongée dans ses rêves.",
  has   animate female;

Object  pere "père"
  with  name 'homme' 'pere' 'proprietaire' 'monsieur' 'garcon' 'jardinier' 'maitre',
        article "le",
        description "Le maître de maison est un homme de haute taille au regard fier et
            intelligent, pour lequel vous avez un grand respect.",
  has   animate male;

Object  garde_entrepot "gardien endormi" couloir_entrepots
  with  name 'garde' 'gardien' 'soldat' 'militaire' 'rat',
        article "le",
        description "Un vieux rat de constitution imposante, qui vous domine de toute sa
            grandeur. Il semble profondément endormi, mais vous préférez rester prudent.",
        before [;
          Attack:
            if (poignard in player) {
              move garde_entrepot to thedark;
              move garde_entrepot_mort to couloir_entrepots;
              score = score - 5;
              "Vous posez un regard sur votre poignard et exprimez une moue de dégout...
              Cependant, saisissant fermement l'arme blanche, vous effectuez votre sale
              besogne d'un geste précis, tailladant profondément la gorge du garde
              endormi.";
            }
            else {
              "Sans arme c'est du suicide. Et tuer un rat dans son sommeil vous répugne.";
            }
          Search: "Vous repérez une clef dorée dans la poche intérieure de la veste du
            gardien. Peut-être pourriez-vous la subtiliser si vous êtes assez discret.";
        ],
  has   animate male transparent;

Object  garde_entrepot_mort "cadavre du gardien de l'entrepôt"
  with  name 'garde' 'gardien' 'soldat' 'militaire' 'rat' 'mort' 'cadavre' 'corps',
        article "le",
        description "La charogne git là, à vos pieds, et vous rappelle votre geste lâche et
            sanguinaire. Peut-être ne valez-vous pas mieux que Kay finalement...",
        before [;
          Smell: "Pas encore d'odeur de décomposition, mais cela viendra bien assez vite.";
          Attack: "Souiller un cadavre ? Peut-être ne faudrait-il pas pousser le vice.";
          Taste,Eat,Drink: "Croque-mort n'a jamais été votre vocation.";
          Listen: "Le silence de la mort n'a jamais été aussi inquiétant.";
          Take,Push,Pull,PushDir: "Aucun intérêt de déplacer le corps.";
          Touch: "Vos pattes sont déjà trop tachées de sang...";
          Search:
            if (clef_entrepot notin player) {
              move clef_entrepot to player;
              "Vous soulevez non sans dégoût la veste tachée de sang du rat gisant et y
              prenez la clef de l'entrepôt.";
            }
          ],
  has   static;

Object  _poignard "poignard d'acier trempé" kay_dortoir
  with  name 'poignard' 'couteau' 'lame' 'acier' 'trempe' 'dague' 'epee',
        description "Une lame en acier trempé de petite taille montée sur une garde à
            votre échelle constitue ce poignard. Vous vous demandez si les humains ont eu
            la hardiesse de concevoir d'aussi petites armes blanches ou si la technologie
            des rats dépasse tout ce que vous pouviez imaginer. Sur la lame est gravé le
            nom Stanley.",
        trouve 0,
        before [;
          Take:
            if (_poignard.trouve == 0) {
              move poignard to player;
              _poignard.trouve = 1;
              print "À peine avez-vous serré vos doigts autour de la garde du poignard
              qu'une patte imposante vient se poser sur votre épaule droite.^^
              ~Quel est cet accoutrement que tu portes, Stanley ?~^^
              Stanley... Ce nom ne vous est pas inconnu. Vous paniquez quelque peu en
              voyant le rat qui vient de vous surprendre, qui devait être assis dans un
              angle quand vous avez inspecté la salle. En croisant son regard inquisiteur
              vous pensez qu'il vaut mieux coopérer jusqu'à ce qu'une occasion se
              présente d'elle-même.^";
              if (heros.etat == 4) { "Vous vous dépétrez non sans mal de l'amas de feuilles
              de papier qui vous recouvre tandis que le visage du rat s'éclaire.^^
              ~Un moment j'ai eu un doute mais je vois que c'est bien toi Stan !~ Et
              l'autre de s'ésclaffer bêtement. ~C'est vrai que des petiots comme toi y'en
              a pas des masses. Fallait bien ça pour le blason, hein ?~^
              Le rat vous tapote l'épaule et repart, rieur."; }
              else if (heros.etat == 5) { "Vous vous dépétrez non sans mal de l'amas de
              feuilles de papier qui vous recouvre et retirez le foulard noir qui vous
              masque le museau, tandis que le visage du rat s'éclaire.^^
              ~Un moment j'ai eu un doute mais je vois que c'est bien toi Stan !~ Et
              l'autre de s'ésclaffer bêtement. ~C'est vrai que des petiots comme toi y'en
              a pas des masses. Fallait bien ça pour le blason, hein ?~^
              Le rat vous tapote l'épaule et repart, rieur."; }
              else if (heros.etat == 3) { "Vous dénouez le foulard noir qui vous masque le
              museau et affichez un sourire gauche, tandis que le visage du rat
              s'éclaire.^^
              ~Un moment j'ai eu un doute mais je vois que c'est bien toi Stan !~ Et
              l'autre de s'ésclaffer bêtement. ~C'est vrai que des petiots comme toi y'en
              a pas des masses. Fallait bien ça pour le blason, hein ?~^
              Le rat vous tapote l'épaule et repart, rieur."; }
            }
            else { <<Take poignard>>; }
          Touch:
            "Un ouvrage délicat, mais assez aiguisé pour trancher à vif une patte de rat,
            sans aucun doute.";
        ],
  has   scenery;

Object  poignard "poignard d'acier trempé"
  with  name 'poignard' 'couteau' 'lame' 'coutelas' 'acier' 'trempe' 'dague' 'epee',
        description "Une lame en acier trempé de petite taille montée sur une garde à
            votre échelle constitue ce poignard. Vous vous demandez si les humains ont eu
            la hardiesse de concevoir d'aussi petites armes blanches ou si la technologie
            des rats dépasse tout ce que vous pouviez imaginer. Sur la lame est gravé le
            nom Stanley.",
        before [;
          Touch:
            "Un ouvrage délicat, mais assez aiguisé pour trancher à vif une patte de rat,
            sans aucun doute.";
        ];

Object  clef_entrepot "clef de l'entrepôt" garde_entrepot
  with  name 'clef' 'cle' 'passe' 'doree',
        article "la",
        description "Une petite clef dorée qui ouvre la porte de l'entrepôt.",
        before [;
          Give: "Ce serait idiot.";
          Steal:
            if (self in garde_entrepot) {
              if (heros.etat == 4 || heros.etat == 5) {
                print "Vous approchez du garde et tendez votre main, mais en frôlant ses
                moustaches, vous éveillez le rat qui, lorsque son regard nauséeux croise
                votre accoutrement immaculé et ridicule, se remet rapidement sur pied et
                sonne l'alarme.";
                deadflag = 5; rtrue;
              }
              if (heros.etat == 3) {
                print "Vous approchez du garde et tendez votre main, mais en frolant ses
                moustaches, vous éveillez le rat qui, tout estourbi, ne vous voit pas
                grâce à votre teinture noire, et se rendort. Vous subtilisez la clef de
                l'entrepot au garde endormi.^";
                move clef_entrepot to player;
                score = score - 1; rtrue;
              }
              else "Veuillez signaler au concepteur que vous êtes parvenus à vous balader
                dans le repaire de Kay sans ressembler à un fantôme ou être tout noir.";
            }
        ],
  has   female;

Object  conserve "boîte de conserve vide" prison
  with  name 'boite' 'conserve' 'vide' 'metal',
        description "Une boîte de métal de tout petit format, qui reste tout de même
            imposante à votre échelle, mais probablement transportable.",
        before [;
          ThrowDir:
            if (second == w_obj && location == couloir_entrepots) {
              if (clef_entrepot notin player) {
                move conserve to couloir_prisons;
                move clef_entrepot to couloir_entrepots;
                score = score + 5;
                "Vous lancez la conserve de toutes vos forces vers les prisons et vous
                glissez dans une zone d'ombre. Le fracas que sa chute provoque réveille
                brusquement le gardien, qui, étonné, s'en va observer la cause du trouble.
                Dans sa hâte, le garde somnolant fait tomber les clefs de l'entrepôt au
                sol.^^
                Il revient une minute plus tard après avoir inspecté le couloir des
                prisons, et se rendort aussitôt à son poste.";
              }
              else {
                "Vous lancez la conserve de toutes vos forces vers les prisons et vous
                glissez dans une zone d'ombre. Le fracas que sa chute provoque réveille
                brusquement le gardien, qui, étonné, s'en va observer la cause du trouble.
                ^^Il revient une minute plus tard après avoir inspecté le couloir des
                prisons, et se rendort aussitôt à son poste.";
              }
            }
          Drop:
            print "Vous jetez la conserve qui s'écrase au sol dans un bruit assourdissant,
            réveillant alors le gardien qui lève lentement son nez sur vous et donne
            l'alerte.";
            deadflag = 5; rtrue;
        ],
  has   female;

Object  porte_entrepot "porte de l'entrepôt"
  with  name 'porte' 'portail' 'lourde' 'bois' 'serrure',
        article "la",
        description "Une lourde porte de bois munie d'une serrure dorée.",
        door_to [;
          if (location == entrepot) return couloir_entrepots;
          else return entrepot;
        ],
        door_dir [;
          if (location == entrepot) return w_to;
          else return e_to;
        ],
        before [;
          Open,Unlock:
            if (clef_entrepot in player) {
              give self open;
              give self ~locked;
              "Vous déverrouillez la porte à l'aide de la clef de l'entrepôt et l'ouvrez.";
            }
        ],
        found_in couloir_entrepots entrepot,
        with_key clef_entrepot,
  has   scenery door openable lockable locked female;

Object  _caisses "caisses" entrepot
  with  name 'boites' 'boite' 'carton' 'vivres' 'pain' 'rassis',
        description "Les boîtes emplies de fromage vous intéressent tout particulièrement
            : vous y repérez reblochon fondant, camembert bien fait et fromage de chèvre,
            mais surtout, et c'est à déplorer, un gruyère... avec trous.",
        gruyere_la 1,
        before [;
          Smell:
            "Les délicieuses fragrances de mets mélangés viennent chatouiller vos
            narines.";
          Taste,Eat:
            "Mieux vaut s'emparer d'un met plutôt que dévorer la caisse qui le contient.";
        ],
  has   scenery pluralname transparent;

Object  gruyere "gruyère à trous" _caisses
  with  name 'gruyere' 'à trou' 'à trous' 'troue' 'emmental' 'moisissure' 'bleue',
        description "Un vrai gruyère suisse, bien que percé par un commercial dans l'ère du
            temps, que les trous dénaturent et sur lequel apparaît une fine couche de
            moisissure bleue.",
        before [;
          Touch,Take:
            if (_caisses.gruyere_la == 1) {
              move gruyere to hotte;
               _caisses.gruyere_la = 0;
              "Vous saisissez le gruyère avec promptitude, qui bien qu'il affiche un
              aspect plutôt rebutant et percé de mille trous, conserve toujours une
              odeur qui chatouille vos narines.";
            }
          Taste,Eat:
            move gruyere to thedark;
            "Vous plantez vos dents dans le morceau alléchant et le dévorez sous peu, y
            trouvant tout de même un arrière-goût désagréable.";
        ],
  has   edible;

Object  fromage "morceaux de fromage" _caisses
  with  name 'fromage' 'quelconque' 'morceaux',
        article "des",
        description "Des morceaux de fromage que vous ne prenez même pas la peine
            d'identifier, tout juste bons pour un rat vu leur aspect et leur odeur.",
        eat_fromage 1,
        before [;
          Touch,Take:
            if (fromage notin player && _caisses.gruyere_la == 0) {
              move fromage to hotte;
              "Ne trouvant plus de gruyère dans les caisses, vous y prenez un fromage
              quelconque.";
            }
            else if (_caisses.gruyere_la == 1) {
              move gruyere to hotte;
              _caisses.gruyere_la = 0;
              "Vous saisissez le gruyère avec promptitude, qui bien qu'il affiche un
              aspect plutôt rebutant et percé de mille trous, conserve toujours une
              odeur qui chatouille vos narines.";
            }
          Taste,Eat:
            if (fromage in player) {
              move fromage to _caisses;
              switch (self.eat_fromage) {
                1: print "Comme vous le présagiez, ce fromage est doté d'un goût atroce.^";
                2: print "Pas meilleur que le précédent.^";
                3: print "Celui-ci à un goût qui rentre dans les clauses du ~correct~.^";
              }
              if (self.eat_fromage == 3) { self.eat_fromage = 1; }
              else { self.eat_fromage++; }
            }
            else if (gruyere in player) { 
              move gruyere to thedark;
              "Vous plantez vos dents dans le morceau alléchant et le dévorez sous peu, y
              trouvant tout de même un arrière-goût désagréable.";
            }
            else { "Quel fromage ?"; }
            rtrue;
          Show,Give:
            if (self in player && second == garde_idiot) {
              if (heros.etat == 4) {
                print "Une idée folle traverse votre esprit. Vous enlevez le
                papier-toilette qui vous recouvre et approchez du garde en arborant un
                sourire éclatant et en marchant en pleine lumière pour bien vous faire
                remarquer.^^";
              }
              if (heros.etat == 5) {
                print "Une idée folle traverse votre esprit. Vous enlevez le
                papier-toilette qui vous recouvre ainsi que le foulard qui couvre votre
                museau et approchez du garde en arborant un sourire éclatant et en
                marchant en pleine lumière pour bien vous faire remarquer.^^";
              }
              if (heros.etat == 3) {
                print "Une idée folle traverse votre esprit. Vous enlevez le foulard qui
                couvre votre museau et approchez du garde en arborant un sourire éclatant
                et en marchant en pleine lumière pour bien vous faire remarquer.^^";
              }
              print "~Qui c'est qui vient ?~ questionne le gros rat. ~Tu veux quoi ?~^^
              Toujours souriant, vous prenez un morceau de fromage et le lui présentez :
              ~Vois-tu, noble ami, j'ai été mandé d'apporter ce met délicieux à notre
              cher patron, Kay. Ce pourquoi j'aimerais passer.~^^
              ~Euh, c'est du poison ?~^^
              Voyons, mon ami le garde de l'entrepôt ne m'aurais pas donné un fromage
              empoisonné pour notre chef. Mais je peux le goûter devant toi si tu veux.~
              Vous avalez un morceau de fromage au goût affreux. Oubliant la prétendue
              raison de votre venue, l'autre s'enquit :^^
              ~Et je peux goûter aussi ?~^^
              Lui donnant les morceaux restants, vous passez la porte pendant qu'il
              dévore l'amas de moisissure, tout en rééquipant votre accoutrement.^";
              move fromage to thedark;
              PlayerTo(salle_reunion);
              rtrue;
            }
        ],
  has   edible pluralname;

Object  garde_idiot "garde idiot" couloir_reunion
  with  name 'garde' 'gardien' 'soldat' 'militaire' 'idiot' 'imbecile' 'bete' 'beat',
        description "Un peu grassouillet, il semble avoir découvert récemment qu'il pouvait
            loucher et faire des bulles avec sa salive simultanément. Cependant il garde
            la porte avec fermeté, et passer ne sera pas aussi facile qu'il est idiot.",
  has   animate male;

Object  _boite_musique "boîte à musique" kay_chambre
  with  name 'bureau' 'secretaire' 'meuble' 'tiroirs' 'boite' 'musique' 'crane' 'oiseau'
            'cranes' 'oisillons' 'oisillon' 'oiseaux',
        description "Une boîte à musique noire, sertie de petites vis dorées, avec une
            manivelle sur son côté droit, vers le lit. Trois crânes d'oisillons sont posés
            sur le couvercle de la boîte, presque contre le mur.",
        before [;
          Take: "Cela n'a visiblement aucun intérêt.";
          Open: <<Turn _manivelle>>;
        ],
  has   scenery female;

Object  _manivelle "manivelle" kay_chambre
  with  name 'manivelle' 'manette',
        description "Une manivelle de bois noir, comme la boîte à musique.",
        before [;
          Turn:
            print "Vous tournez la manivelle non sans difficulté, et le capot se soulève
            lentement, faisant rouler les crânes encore plus contre le mur dans un bruit
            étouffé. Une musique effrayante, jouant des basses et des gammes mineures
            comme un jongleur avec ses balles, en jaillant, tandis que vous découvrez à
            l'intérieur de la boîte un carnet usé, qui se révèle être le journal de Kay.
            Vous vous en saisissez et refermez le couvercle de la boîte.^";
            move journal_kay to hotte;
            rtrue;
        ],
  has   scenery female;

Object  journal_kay "journal de Kay" _manivelle
  with  name 'journal' 'intime' 'log' 'carnet' 'use',
        article 'le',
        read 0,
        description [;
          if (self.read == 0) {
            self.read = 1;
            print "Vous feuilletez attentivement le carnet qui liste tous les méfaits et
            états d'âme de Kay, et se termine ainsi :^^"; style underline; print "Jour 22
            de l'année du Reblochon,^^J'avais intuité que la souris Stanley me serait
            utile tôt ou tard, voilà qui est chose faite : l'ancien Chevalier de Mice VII,
            avide de vengeance, m'a apporté le blason de la famille royale sans que je
            n'aie besoin de rien lui demander. Il s'est par cet acte offert une place
            parmi nous, bien que je continue de le surveiller.^
            Vu la valeur de l'artefact, je vais le placer au dessus de la porte de la
            salle de réunion, ainsi je l'aurais toujours en vue.^^"; style roman; print
            "Voilà donc l'information qu'à la fois vous attendiez et redoutiez. Le voleur
            est bien Stan, qui faisait partie du trio que vous formiez avec le roi et qui
            vous semblait, alors jeunes souriceaux, inébranlable. Mais voilà plusieurs
            lunes que Stanley a quitté le palais suite à une altercation avec son ancien
            ami le roi Mice VII, et vous êtiez jusqu'à aujourd'hui sans nouvelles.^";
            Achieved(3);
            heros.task3 = 1;
            rtrue;
          }
          else { "Le journal de Kay témoigne de l'implication directe de votre ancien ami
            Stanley dans le vol du blason royal."; }
        ],
        before [;
          Drop,ThrowDir,ThrowAt: "Il serait bête de se séparer d'une de vos preuves.";
        ];

Object  lampe "petite lampe pour bicyclette" kay_chambre
  with  name 'lampe' 'bicyclette' 'velo' 'lampion' 'petite' 'lumiere',
        description "Une petite lampe plutôt plate et circulaire, qui éclaire avec une
            puissance étonnant pour son format. On l'allume en appuyant dessus avec
            force.",
        allume 0,
        before [;
          SwitchOn:
            if (self.allume == 20) { "Plus d'énergie..."; }
            else {
              give self light;
              "Vous allumez la lampe qui éclaire brillament le lieu.";
            }
          SwitchOff:
            if (self.allume == 20) { "Pourquoi éteindre ce qui l'est déjà ?"; }
            else { 
              give self ~light;
              "Vous éteignez la lampe.";
            }
          Take:
            if (self has scenery) {
              give self ~scenery;
              move lampe to hotte;
              "Vous vous dressez sur le bout de vos pattes et détachez la lampe du plafond
              de terre pour vous en emparer.";
            }
        ],
        each_turn [;
          if (self has light) {
            self.allume = self.allume + 1;
            if (self.allume == 20) {
              give self ~light;
              "La lampe s'éteint : il n'y a plus d'énergie.";
            }
          }
        ],
  has   scenery female;

!=======================================================================================

[ Initialise;
  lookmode = 2;
  ChangePlayer(heros);
  location = trou_dortoirs;
  print "Si vous jouez pour la première fois à une fiction interactive, tapez "; style underline; print "aide.^^";
];

[ PrintTaskName task_number;
  switch (task_number) {
      0: "points - s'introduire chez le rat Kay";
      1: "points - récupérer le blason royal";
      2: "points - rapporter le blason au roi Mice VII";
      3: "points - identifier le voleur (facultatif)";
  }
];

[ DeathMessage;
  if (deadflag == 3) print "Vous êtes dévoré par la sanguinaire Choupinette.";
  if (deadflag == 4) print "Vous gisez au sol, et décédez suite à une longue agonie.";
  if (deadflag == 5) print "Vous finissez vos jours au fond d'une oubliette.";
];

[ SalonChoupinette;
  print "C'était à prévoir, le centre du salon, totalement à découvert, est le
  domaine de la terrifiante Choupinette, et vous n'avez pas le temps de prendre
  conscience du danger que déjà, vous constituez un repas succulent.";
  deadflag = 3;
];

[ HelpSub;
  print "Ce programme est un jeu d'aventure textuel, ou "; style underline; print
  "fiction interactive"; style roman; print " et se base donc sur un dialogue entre
  le joueur, vous, et sa machine. Ainsi vous pouvez interagir avec le milieu
  environnant à travers des ordres à l'impératif ou à l'infinitif, tels que ceux-ci :^";
  print " > prendre la hotte^ > manger le gruyere^ > regarder le roi^^
  Il peut arriver (voir assez souvent pour un joueur néophyte) que vos instructions ne
  soient pas comprises, vous pouvez alors réessayer en reformulant d'une manière plus
  claire et simple (l'association "; style underline; print "verbe"; style roman; print
  " + "; style underline; print "complément"; style roman; print " est la plus
  courante).^^";
  print "Pour déplacer votre personnage de lieux en lieux, utilisez les huits points
  cardinaux ("; style underline; print "nord, nord-est, est, sud-est, sud, sud-ouest,
  ouest, nord-ouest, nord"; style roman; print ") ainsi qu'"; style underline; print
  "entrer, sortir, haut, bas"; style roman; print ".^Pour avoir de nouveau la description
  d'un lieu qui apparaît lorsque l'on s'y rend, tapez "; style underline; print
  "regarder"; style roman; print " lorsque vous vous y trouvez.^Pour lister vos
  acquisitions, tapez "; style underline; print "inventaire"; style roman; print " et
  enfin en rapport avec votre progression, utilisez "; style underline; print "score"; style
  roman; print " ou "; style underline; print "fullscore"; style roman; print " pour en avoir
  connaissance, ainsi que les commandes "; style underline; print "sauvegarder, charger";
  style roman; print " et "; style underline; print "recommencer"; style roman; print ".^^
  Pour plus d'informations, les commandes "; style underline; print "licence"; style roman;
  print " et "; style underline; print "auteur"; style roman; print " sont disponibles,
  et le site "; style underline; print "http://ifiction.free.fr"; style roman; print " est
  à votre disposition.^";
];

[ AuthorSub;
  print "Ce jeu est la première réalisation personnelle de Rémi Verschelde (Akien),
  réalisée du 4 août 2008 au 10 août 2008 pour sa première version. Le but de cette
  création était principalement d'entraîner son auteur au langage de programmation ";
  style underline; print "Inform"; style roman; print " en le confrontant directement aux
  difficultés, ce pourquoi vous pouvez peut-être estimer que le scénario est simple,
  manque de clarté ou d'intérêt, n'étant pas son intérêt premier.^^
  Le site de la communauté francophone de fiction interactive ("; style underline; print 
  "http://ifiction.free.fr"; style roman; print ") offre de nombreuses autres réalisations
  d'auteurs francophones talentueux, et vous pourrez en retrouver beaucoup sur son forum,
  de même que l'auteur de ce jeu, également joignable par courriel à l'adresse suivante :^^
  rverschelde "; style bold; print "CHEZ"; style roman; print " gmail "; style
  bold; print "POINT"; style roman; print " com.^";
];

[ LicenseSub;
  print "Licence BSD^
  Copyright (c) 2008 Rémi Verschelde (Akien) - http://ifiction.free.fr^^
  Tous droits réservés.^
  La redistribution et l'utilisation sous forme binaire ou de code source,
  avec ou sans modification, est autorisée à condition que les conditions 
  suivantes soient respectées:^^
  1. Les redistributions du code source doivent conserver l'indication de 
   copyright ci-dessus, cette liste de conditions et la renonciation suivante.^ 
  2. Les redistributions sous forme binaire doivent reproduire l'indication 
   de copyright ci-dessus, cette liste de conditions et la renonciation suivante 
   dans la documentation et/ou les autres accessoires fournis avec la distribution.^ 
  3. Toutes les publicités mentionnant les caractéristiques ou l'utilisation 
   de ce logiciel doivent mentionner la phrase suivante : 
   ~Ce produit inclus un logiciel et du travail développés par Rémi Verschelde (Akien) et
   la communauté de la fiction interactive francophone (http://ifiction.free.fr/) 
    et internationale, ainsi que par les créateurs du système INFORM 
   (http://www.inform-fiction.org).~^
  4. Les noms des créateurs et développeurs ne peuvent être utilisés pour 
   approuver ou promouvoir les produits dérivés de ce logiciel sans une 
   permission écrite spécifique préalable.^^
  CE LOGICIEL EST FOURNI PAR LES TITULAIRES DU COPYRIGHT ET LES PARTICIPANTS
  ~TEL QUEL~ ET TOUTES LES GARANTIES EXPLICITES OU IMPLICITES, Y COMPRIS, 
  MAIS NON LIMITÉES A CELLES-CI, LES GARANTIES IMPLICITES DE COMMERCIALISATION 
  ET D'ADÉQUATION À UN USAGE PARTICULIER SONT DÉNIÉES.
  EN AUCUN CAS LES TITULAIRES DU COPYRIGHT OU LES PARTICIPANTS NE PEUVENT 
  ÊTRE TENUS POUR RESPONSABLE DES DOMMAGES DIRECTS, INDIRECTS, FORTUITS, 
  PARTICULIERS, EXEMPLAIRES OU CONSÉCUTIFS À UNE ACTION (Y COMPRIS, MAIS NON 
  LIMITÉS À CEUX-CI, L'ACQUISITION DE MARCHANDISES OU DE SERVICES DE REMPLACEMENT; 
  LES PERTES D'UTILISATIONS, DE DONNÉES OU FINANCIÈRES; OU L'INTERRUPTION 
  D'ACTIVITÉS) DE QUELQUE MANIÈRE QUE CES DOMMAGES SOIENT CAUSÉS ET CECI 
  POUR TOUTES LES THÉORIES DE RESPONSABILITÉS, QUE CE SOIT DANS UN CONTRAT, 
  POUR DES RESPONSABILITÉS STRICTES OU DES PRÉJUDICES (Y COMPRIS DUS À UNE 
  NEGLIGENCE OU AUTRE CHOSE) SURVENANT DE QUELQUE MANIÈRE QUE CE SOIT EN DEHORS 
  DE L'UTILISATION DE CE LOGICIEL, MÊME EN CAS D'AVERTISSEMENT DE LA POSSIBILITÉ 
  DE TELS DOMMAGES.^";
];

[ StealNounSub;
  "Qui ou que voulez-vous voler ?";
];

[ StealSub;
  "Vous n'avez aucune raison de voler cette personne.";
];

[ ThrowDirSub;
  "Tenez vos mains !";
];

!=======================================================================================

Global ScenicFlag = 4;
Include "scenic5sens";

! Version 1.0

Include "FrenchG";

! Version 2.2 des bibliothèques francophones

Verb "help" "aide" "manuel" "info" "information"
    *               -> Help;

Verb "auteur" "credits"
    *               -> Author;

Verb "licence" "license" "droits"
    *               -> License;

Verb "voler" "subtiliser" "vole" "subtilise" "derober" "derobe" "piquer" "pique"
    * noun                                                    -> StealNoun
    * noun creature                                           -> Steal
    * noun 'a'/'à'/'au'/'aux'/'de'/'du'/'des' creature        -> Steal;

Extend "prendre" last
    * noun creature                                           -> Steal
    * noun 'a'/'à'/'au'/'aux'/'de'/'du'/'des' creature        -> Steal;

Extend "jeter" last
    * noun 'a'/'à'/'au'/'vers' noun=ADirection                -> ThrowDir;

!=======================================================================================